plugins {
    id 'maven-publish'
}

//description = 'Crow Game Engine'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(path: ':screen')
    api project(path: ':shared')
    implementation project(path: ':input')
    implementation project(path: ':game')
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.named('jar') {
    enabled = true
    // avoid the "-plain" suffix that may cause compatibilities issues with maven
    archiveClassifier = ''

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    dependsOn 'javadoc'

    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

tasks.register('fatJar', Jar) {
    dependsOn(':shared:jar', ':screen:jar', ':game:jar', ':audio:jar', ':input:jar')

    manifest.from jar.manifest
    // TODO: Review this. It is including third-part files and maybe replacing ours.
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

artifacts {
    archives fatJar
}

publishing {
    repositories {
        maven {
            name = 'nexus'
            url = 'http://localhost:8010/repository/maven-releases/'
            allowInsecureProtocol = true
            credentials {
                username = System.getenv("NEXUS_USER")
                password = System.getenv("NEXUS_PASS")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'crow-engine'
//            from components.java
            artifact fatJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Crow Engine'
                description = 'A game 2D engine for java game development.'
                url = 'https://github.com/dendriel/crow-framework'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/mit/'
                    }
                }
                developers {
                    developer {
                        id = 'dendriel'
                        name = 'Vitor Rozsa'
                        email = 'vitor.rozsa@hotmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:dendriel/crow-framework.git'
                    developerConnection = 'scm:git:git@github.com:dendriel/crow-framework.git'
                    url = 'https://github.com/dendriel/crow-framework'
                }
            }
        }
    }
}
